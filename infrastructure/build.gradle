buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.avast.gradle.docker-compose" version "0.9.1"
}

apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-allopen'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar.enabled = false
jar.enabled = true

dependencies {
    implementation project(':domain')
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '1.1.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'junit'
        exclude module: 'mockito-core'
    }
}

dockerCompose {
    captureContainersOutput = false
    useComposeFiles = ['src/test/resources/docker-compose.yml']
    stopContainers = true
    removeContainers = true

}

dockerCompose.isRequiredBy(test)

test.doFirst {
    dockerCompose.exposeAsEnvironment(test)
    dockerCompose.exposeAsSystemProperties(test)
}